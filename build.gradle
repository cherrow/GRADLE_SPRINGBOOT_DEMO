plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '2.0.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'PACKAGE'
            includes = ['com.cherrow.spring.service',
                        'com.cherrow.spring.controller']

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1
            }
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1
            }
        }
    }
}

dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    implementation 'org.springframework.boot:spring-boot-dependencies:2.0.5.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.projectlombok:lombok:1.18.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'junit:junit:4.12'
}

// In this section you declare where to find the dependencies of your project
repositories {
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
    mavenCentral()
    jcenter()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

check.dependsOn jacocoTestCoverageVerification
check.dependsOn jacocoTestReport